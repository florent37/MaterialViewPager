apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.ben-manes.versions'

version = project.MAVEN_VERSION

android {
    compileSdkVersion project.ANDROID_BUILD_SDK_VERSION
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion project.ANDROID_BUILD_MIN_SDK_VERSION
        targetSdkVersion ANDROID_BUILD_TARGET_SDK_VERSION
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile (
            'com.android.support:appcompat-v7:'+project.SUPPORT_VERSION,
            'com.android.support:support-v4:'+project.SUPPORT_VERSION,
            'com.android.support:recyclerview-v7:'+project.SUPPORT_VERSION,
            'com.android.support:cardview-v7:'+project.SUPPORT_VERSION,
            'com.android.support:support-annotations:'+project.SUPPORT_VERSION
    )

    compile 'com.nineoldandroids:library:2.4.0'

    compile 'com.flaviofaria:kenburnsview:1.0.6'
    compile 'com.jpardogo.materialtabstrip:library:1.1.0'
    compile 'com.github.ksoichiro:android-observablescrollview:1.5.2'
    compile 'com.squareup.picasso:picasso:2.5.2'
}


//https://github.com/danielemaddaluno/gradle-jcenter-publish

// ./gradlew install bintrayUpload


def siteUrl = 'https://github.com/florent37/MaterialViewPager'      // Homepage URL of the library
def gitUrl = 'https://github.com/florent37/MaterialViewPager.git'   // Git repository URL
group = "com.github.florent37"                      // Maven Group ID for the artifact


install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'

                // Add your description here
                name 'MaterialViewPager'
                description = ''
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'florent37'
                        name 'Florent Champigny'
                        email 'champigny.florent@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

Properties properties = new Properties()
File f = new File("local.properties")
if(f.exists()) {
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
}

// https://github.com/bintray/gradle-bintray-plugin
bintray {

    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        // it is the name that appears in bintray when logged
        name = "MaterialViewPager"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
        version {
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
                passphrase = properties.getProperty("bintray.gpg.password") //Optional. The passphrase for GPG signing'
            }
//            mavenCentralSync {
//                sync = true //Optional (true by default). Determines whether to sync the version to Maven Central.
//                user = properties.getProperty("bintray.oss.user") //OSS user token
//                password = properties.getProperty("bintray.oss.password") //OSS user password
//                close = '1' //Optional property. By default the staging repository is closed and artifacts are released to Maven Central. You can optionally turn this behaviour off (by puting 0 as value) and release the version manually.
//            }
        }
    }
}